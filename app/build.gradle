plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    signingConfigs {
        debug {
            storeFile file('/Volumes/TRS-83/dev/OAuth2Compose-Private/OAuth2Compose.keystore')
            storePassword 'password'
            keyAlias 'key0'
            keyPassword 'password'
        }
        release {
            storeFile file('/Volumes/TRS-83/dev/OAuth2Compose-Private/OAuth2Compose.keystore')
            storePassword 'password'
            keyAlias 'key0'
            keyPassword 'password'
        }
    }
    namespace 'com.realityexpander.oauth2compose'
    compileSdk 33
    android.buildFeatures.buildConfig true

    defaultConfig {
        applicationId android.namespace //"com.realityexpander.oauth2compose"
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        manifestPlaceholders = [
            // note: could be any string, but by convention we use the package name
            'appAuthRedirectScheme': android.namespace //'com.realityexpander.oauth2compose'
        ]

        // Load local.properties
        def localProperties = new Properties()
        def localPropertiesFile = rootProject.file('local.properties')
        if (localPropertiesFile.exists()) {
            localPropertiesFile.withReader('UTF-8') { reader ->
                localProperties.load(reader)
            }
        }
        // Add BuildConfig fields
        buildConfigField "String", "GOOGLE_CLIENT_ID_ANDROID", "\"${localProperties['GOOGLE_CLIENT_ID_ANDROID']}\""
        buildConfigField "String", "GOOGLE_CLIENT_ID_WEB_APPLICATION", "\"${localProperties['GOOGLE_CLIENT_ID_WEB_APPLICATION']}\""
        buildConfigField "String", "GITHUB_CLIENT_ID", "\"${localProperties['GITHUB_CLIENT_ID']}\""
        buildConfigField "String", "GITHUB_CLIENT_SECRET", "\"${localProperties['GITHUB_CLIENT_SECRET']}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Networking
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    // OpenAuth / OAuth2 Client
//    implementation 'net.openid:appauth:0.7.0'
    implementation 'net.openid:appauth:0.11.1'

    // Google OAuth2
    implementation 'com.google.android.gms:play-services-auth:20.4.1'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0'

    // Image loading with Coil
    implementation "io.coil-kt:coil-compose:2.2.2"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}